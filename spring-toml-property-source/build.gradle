plugins {
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-toml:$jacksonVersion"
    compileOnly "org.springframework.boot:spring-boot"
    implementation "org.springframework.integration:spring-integration-core"
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'spring-toml-property-source'
                description = 'A Spring library for loading TOML configurations.'
                url = 'https://github.com/OmarAssadi/spring-toml-property-source'
                licenses {
                    license {
                        name = 'ISC License'
                        url = 'https://opensource.org/licenses/ISC'
                    }
                }
                developers {
                    developer {
                        id = 'omar'
                        name = 'Omar Assadi'
                        email = 'omar@assadi.co.il'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/OmarAssadi/spring-toml-property-source.git'
                    developerConnection = 'scm:git:ssh://github.com/OmarAssadi/spring-toml-property-source.git'
                    url = 'https://github.com/OmarAssadi/spring-toml-property-source'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol=true
            credentials(PasswordCredentials)
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
